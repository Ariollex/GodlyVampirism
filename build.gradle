buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
    }
}

plugins {
    id 'com.matthewprenger.cursegradle' version '1.2.0'
}



apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'idea'
apply plugin: 'eclipse'



configurations {
    optionalRuntime
    runtime.extendsFrom optionalRuntime // optionalRuntime dependencies are downloaded but not exported to POM
}

version = project.minecraft_version + "-" + project.mod_version
group = "de.teamlapen.godlyvampirism" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "GodlyVampirism"

sourceCompatibility = targetCompatibility = "1.8" // Need this here so eclipse task generates correctly.
compileJava {
    sourceCompatibility = targetCompatibility = "1.8"
}

repositories {
    //Maven repo for Vampirism
    maven {
        name = "MaxanierMaven"
        url = "https://maxanier.de/maven2"
    }
    maven {
        // location of the maven that hosts JEI files
        name = "Progwml6 maven"
        url = "http://dvs1.progwml6.com/files/maven"
    }
    maven {
        // location of a maven mirror for JEI files, as a fallback
        name = "ModMaven"
        url = "modmaven.k-4u.nl"
    }
    maven{
        //My custom location for (temp) local builds
        url = 'file://' + project.file('../../../.m2/repository').getAbsolutePath()
    }

}
dependencies {
    minecraft([
            group: "net.minecraftforge",
            name: "forge",
            version: "${project.minecraft_version}-${project.forge_version}"
    ])
    
    //compile against Vampirism. Version is taken from gradle.properties. We have to separate dependencies here, to trick gradle into not exporting the "_deobf" file name to pom
    compileOnly fg.deobf("de.teamlapen.vampirism:Vampirism:${minecraft_version}-${vampirism_version}")
    // 
    runtimeOnly fg.deobf("de.teamlapen.vampirism:Vampirism:${minecraft_version}-${vampirism_version}")

}


minecraft {
    mappings channel: "${project.mcp_channel}".toString(), version: "${project.mcp_mappings}".toString()

    runs {
        client {
            taskName 'runClient1'
            main 'net.minecraftforge.userdev.LaunchTesting'
            property 'forge.logging.console.level', 'debug'
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'mixin.env.disableRefMap', 'true'
            workingDirectory project.file('run/client1').canonicalPath
            
            mods {
                "godly-vampirism" {
                    source sourceSets.main
                }
            }

        }
        server = {
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            property 'mixin.env.disableRefMap', 'true'

            workingDirectory project.file('run/server').canonicalPath

            mods {
                'godly-vampirism' {
                    source sourceSets.main
                }


            }
        }
    }

}


processResources {

    from(sourceSets.main.resources.srcDirs) {
        include 'META-INF/mods.toml'

        expand 'version': project.mod_version, 'mcversion': minecraft_version, 'forge_version': forge_version, 'vampirism_version': vampirism_version
    }



    from(sourceSets.main.resources.srcDirs) {
        exclude 'META-INF/mods.toml'
    }
    outputs.upToDateWhen { false }

    exclude '**/Thumbs.db'
}


curseforge {
    if (project.hasProperty("CURSEFORGE_API")) {
        apiKey = project.CURSEFORGE_API
    } else {
        apiKey = "DUMMY"
    }
    project {
        id = "441422"
        releaseType = 'release'

        addGameVersion project.minecraft_version
        if(project.minecraft_version=="1.16.4"){
            addGameVersion "1.16.5"
        }

        mainArtifact(jar) {
            displayName = "GodlyVampirism-${project.minecraft_version}-${project.mod_version}"
        }

        relations {
            requiredDependency 'vampirism-become-a-vampire'
        }

    }


}
